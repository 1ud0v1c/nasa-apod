import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.ludovic.vimont.nasaapod"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 6
        versionName "1.2.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildFeatures {
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                ignoreFailures false
                testLogging {
                    events "passed", "skipped", "failed", "standardOut", "standardError"
                }
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

// https://medium.com/@wasyl/pretty-tests-summary-in-gradle-744804dd676c
tasks.withType(Test) {
    testLogging {
        events  TestLogEvent.FAILED, TestLogEvent.PASSED,
                TestLogEvent.SKIPPED, TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

        debug {
            events  TestLogEvent.STARTED,
                    TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_ERROR,
                    TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) {
                def resultType = result.resultType
                def numberOfTests = result.testCount
                def succeedTests = result.successfulTestCount
                def failedTests = result.failedTestCount
                def skippedTests = result.skippedTestCount

                def output = "Results: $resultType ($numberOfTests tests, $succeedTests successes, $failedTests failures, $skippedTests skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()

                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Android
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.recyclerview:recyclerview:1.1.0"

    // SnackBar
    implementation "com.google.android.material:material:1.3.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // ViewModel
    implementation "androidx.fragment:fragment-ktx:1.3.0"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Preferences
    implementation "androidx.preference:preference-ktx:1.1.1"

    // Firebase
    implementation "com.google.firebase:firebase-analytics:18.0.2"
    implementation "com.google.firebase:firebase-crashlytics:17.3.1"

    // Retrofit for API call
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // Glide for Image loading
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Kotlin coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    // Room DB
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Dependency Injection
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    // WorkerManager with coroutine support
    implementation "androidx.work:work-runtime:$worker_version"
    implementation "androidx.work:work-runtime-ktx:$worker_version"

    // PhotoView
    implementation "com.github.chrisbanes:PhotoView:2.3.0"

    // RoundedImageView, to help fix an issue with Glide @see: https://github.com/bumptech/glide#compatibility
    implementation "com.makeramen:roundedimageview:2.3.0"

    // Unit tests
    testImplementation "junit:junit:4.13.1"
    testImplementation "org.robolectric:robolectric:4.4"
    testImplementation "androidx.test:core:1.3.0"
    testImplementation "org.koin:koin-test:$koin_version"
    testImplementation "androidx.work:work-testing:$worker_version"

    // UI tests
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation("com.schibsted.spain:barista:3.7.0") {
        exclude group: "org.jetbrains.kotlin"
    }
    androidTestImplementation "androidx.navigation:navigation-testing:$navigation_version"
}